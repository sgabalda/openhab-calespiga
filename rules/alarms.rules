import org.openhab.core.model.script.ScriptServiceUtil
import java.time.ZonedDateTime
import java.time.temporal.ChronoUnit

rule "Alarmes inicialitzaciÃ³"
when   
    System started
then
    Alarms.members.forEach[ i | i.postUpdate(OFF) ]
    AlarmsTitle.sendCommand(OFF)
end

rule "Alarma canvia estat"
when
    Member of Alarms changed
then
    val alarmItem = triggeringItem
    val alarmPreviousState = previousState
    val alarmNextState = newState

    val now = ZonedDateTime::now
    val nowDateType = new DateTimeType(now) 

    if(alarmPreviousState == OFF && alarmNextState == ON){
        AlarmsTitle.sendCommand(ON)
        val alarmTimestampOn = ScriptServiceUtil.getItemRegistry.getItem(alarmItem.name + "TimeOn")
        logDebug("Alarms", "Alarm "+alarmItem.name+" has been fired") 
        if(alarmTimestampOn !== null ) {
            alarmTimestampOn.postUpdate(nowDateType)
            logDebug("Alarms", "Alarm timestamp updated to now") 
        }else{
            logError("Alarms", "Alarm "+alarmItem.name+" has not timestamp for ON") 
        }
        val alarmNotificationItem = ScriptServiceUtil.getItemRegistry.getItem(alarmItem.name + "Notification")
        if(alarmNotificationItem !== null ) {
            logDebug("Alarms", "Found alarm notification item for "+alarmItem.name)
            if(alarmNotificationItem.state == ON){
                logDebug("Alarms", "Alarm notification item is ON for "+alarmItem.name) 
                val alarmTextItem = ScriptServiceUtil.getItemRegistry.getItem(alarmItem.name + "Text")
                if(alarmTextItem !== null ) {
                    logDebug("Alarms", "Found alarm text for "+alarmItem.name) 
                    var timeBetweenNotificationsMinutes = 10L
                    val alarmTimeNotifItem = ScriptServiceUtil.getItemRegistry.getItem(alarmItem.name + "TimeNotifications")
                    if(alarmTimeNotifItem !== null ) {
                        val alarmTimeNotifItemState = alarmTimeNotifItem.state
                        if(alarmTimeNotifItemState != NULL){
                            timeBetweenNotificationsMinutes = (alarmTimeNotifItemState as Number).longValue
                            logDebug("Alarms", "Time between notifications is "+timeBetweenNotificationsMinutes+" for alarm "+alarmItem.name) 
                        }else{
                            logWarn("Alarms", "Time between notifications is NULL, using the default 10 min but something is misconfigured for alarm "+alarmItem.name)
                        } 
                    }else{
                        logError("Alarms", "Alarm "+alarmItem.name+" has no Time item. Using the default!")
                    }
                    var lastNotifiedMinutesAgo = timeBetweenNotificationsMinutes + 1L
                    val alarmLastNotifItem = ScriptServiceUtil.getItemRegistry.getItem(alarmItem.name + "LastNotificationTime")
                    logDebug("Alarms", "Got last notified item for alarm "+alarmItem.name) 
                    if(alarmLastNotifItem !== null ) {
                        val alarmLastNotifItemState = alarmLastNotifItem.state
                        logDebug("Alarms", "Last notified item state is "+alarmLastNotifItemState+" for alarm "+alarmItem.name)
                        if(alarmLastNotifItemState != NULL){
                            val lastConnection = (alarmLastNotifItemState as DateTimeType).getZonedDateTime()
                            lastNotifiedMinutesAgo = ChronoUnit.MINUTES.between(lastConnection, now);
                            logDebug("Alarms", "Last notified time is "+lastNotifiedMinutesAgo+"m ago, for alarm "+alarmItem.name)
                        }else{
                            logWarn("Alarms", "Last notified time is NULL, if it is not the first time maybe it is not properly updating for alarm "+alarmItem.name)
                        }
                    }else{
                        logError("Alarms", "Alarm "+alarmItem.name+" has no LastNotification item. Can not filter notifications!")
                    }
                    if(lastNotifiedMinutesAgo > timeBetweenNotificationsMinutes){
                        if(alarmLastNotifItem !== null ) {
                            alarmLastNotifItem.postUpdate(nowDateType)
                            logDebug("Alarms", "Alarm last notified updated for alarm "+alarmItem.name)
                        }else{
                            logError("Alarms", "Alarm "+alarmItem.name+" has no LastNotification item. Can not record last notification time!")
                        }
                        logInfo("Alarms", "Alarm "+alarmItem.name+" sending the notification, as it has not been sent since "+timeBetweenNotificationsMinutes+"m ago") 
                        sendBroadcastNotification(alarmTextItem.label)
                    }else{
                        logDebug("Alarms", "Alarm "+alarmItem.name+" NOT sending the notification, as it was sent less than "+timeBetweenNotificationsMinutes+"m ago") 
                    }
                }else{
                    logError("Alarms", "Alarm "+alarmItem.name+" has no Text item. Can not send broadcast!")
                }
            }else{
                logDebug("Alarms", "Alarm "+alarmItem.name+" has notifications disabled, not sending") 
            }
        }else{
            logError("Alarms", "Alarm "+alarmItem.name+" has no Notification item. Can check if have to send broadcast!")
        }
    }else if(alarmPreviousState == ON && alarmNextState == OFF){
        logDebug("Alarms", "Alarm "+alarmItem.name+" has stopped") 
        var actuator = Alarms.members.findFirst[state == ON]
        if (actuator!==null){
            logDebug("Alarms", "There is still some alarm on, leaving the general state alarm as ON") 
            AlarmsTitle.sendCommand(ON)
        } else {
            logDebug("Alarms", "There is no more alarm on, leaving the general state alarm as OFF") 
            AlarmsTitle.sendCommand(OFF)
        }
        val alarmTimestampOff = ScriptServiceUtil.getItemRegistry.getItem(alarmItem.name + "TimeOff")
        if(alarmTimestampOff !== null ) {
            logDebug("Alarms", "Updating the OFF timestamp for alarm "+alarmItem.name) 
            alarmTimestampOff.postUpdate(nowDateType)
        }else{
            logError("Alarms", "Alarm "+alarmItem.name+" has not timestamp for OFF") 
        }
        logDebug("Alarms", "Alarm "+alarmItem.name+" has been stopped") 
    }else{
        logDebug("Alarms", "Alarm "+alarmItem.name+" has been updated but not changed") 
    }
end