import java.util.Date
import java.time.ZonedDateTime

// used to notify if the heater is hot for too much time
var lastChangeToHot = 0L
var timeHotToNotify = 60 * 60 * 1000L * 2   // 2hours

// used to notify if the mqtt commands do not receive response
var Timer responseNotReceivedTimer = null

rule "Notifica Massa Temps Hot"
when
    Item Calentador_Termostat_UserDisplay received update
then
    val receivedEstat=Calentador_Termostat_UserDisplay.state.toString
    logDebug("Calentador_notificacions.rules","Estat calentador updated: "+receivedEstat)
    //If the state is 
    if(receivedEstat == "HOT"){
        var nowmill=(new Date()).time
        logDebug("Calentador_notificacions.rules", "Current time is "+nowmill) 
        //if last change to hot is not set, this is the first time it changes
        if(lastChangeToHot == 0){
            lastChangeToHot = nowmill
            logDebug("Calentador_notificacions.rules", "Starting to count time HOT") 
        }else{
            //if the last notification was no longer ago than timeBetweenNotifications, don't notify
            if(timeHotToNotify > nowmill - lastChangeToHot){
                logDebug("Calentador_notificacions.rules", "Time HOT is not yet enough to send notification")
            }else{
                sendBroadcastNotification("Calentador està HOT durant molt de temps. Potser està el termostat tancat?") 
                logDebug("Calentador_notificacions.rules", "Send the notification") 
                lastChangeToHot = nowmill  //reset it so the notification will be sent again
            }
        }
    }else{
        logDebug("Calentador_notificacions.rules", "HEATER - State is not hot, resetting the timer") 
        lastChangeToHot = 0
    }
end

rule "Inicia timer alarma no comunicacio amb calentador"
when
    Item Calentador_Potencia_ProxyItem received command
then
    if(responseNotReceivedTimer!==null){
        logDebug("Calentador_notificacions.rules", "The HEATER response alert is already set. Not setting another") 
    }else{
        logDebug("Calentador_notificacions.rules", "configuring HEATER response alert") 
        val secondsItem = AlarmCalentadorCommunication_Seconds.state
        var timeForReceivingResponse = 60L
        if(secondsItem != NULL){
           timeForReceivingResponse = (secondsItem as Number).intValue
           logDebug("Calentador_notificacions.rules", "setting the time for HEATER response alert to "+timeForReceivingResponse+"s") 
        }else{
            AlarmCalentadorCommunication_Seconds.sendCommand(60)
            logDebug("Calentador_notificacions.rules", "setting the time for HEATER response alert to the default "+timeForReceivingResponse+"s") 
        }
        val now = ZonedDateTime::now
        responseNotReceivedTimer = createTimer(now.plusSeconds(timeForReceivingResponse), [|
                logDebug("Calentador_notificacions.rules", "HEATER response timeout reached") 
                responseNotReceivedTimer = null
                AlarmCalentadorCommunication.postUpdate(ON)
            ])
    }

end

rule "Cancela timer alarma no comunicacio amb calentador"
when
    Item Calentador_Potencia_UserDisplay received update
then
    logDebug("Calentador_notificacions.rules", "Response received from the heater, canceling the timer")
    AlarmCalentadorCommunication.postUpdate(OFF)
    if(responseNotReceivedTimer!==null){
        responseNotReceivedTimer.cancel()
        responseNotReceivedTimer=null
    }
end